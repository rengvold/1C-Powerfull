#Область ПрограммныйИнтерфейс

	Функция ПодготовитьАрхив() Экспорт
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Товары.Ссылка)) КАК Идентификатор,
			|	ПРЕДСТАВЛЕНИЕ(Товары.Ссылка) КАК Товар,
			|	Товары.Фото КАК Фото
			|ИЗ
			|	Справочник.Товары КАК Товары
			|ГДЕ
			|	НЕ Товары.ЭтоГруппа";
				
		Выборка = Запрос.Выполнить().Выбрать();
		
		ИмяКаталога = ПолучитьИмяВременногоФайла("");
		СоздатьКаталог(ИмяКаталога);
		
		ОписаниеТоваров = Новый Массив;
	
		ШаблонИмениКартинки = "%1%2%3.jpg";
		
		Пока Выборка.Следующий() Цикл
		
			ОписаниеТовара = Новый Структура;
			ОписаниеТовара.Вставить("Идентификатор", Выборка.Идентификатор);
			ОписаниеТовара.Вставить("Товар", Выборка.Товар);
			
    		ДанныеФото = Выборка.Фото.Получить();
			
			Если ДанныеФото = Неопределено Тогда
				ОписаниеТовара.Вставить("Фото", Неопределено);		
			Иначе
				ИмяФайла = СтрШаблон(ШаблонИмениКартинки, ИмяКаталога, ПолучитьРазделительПути(), Выборка.Идентификатор);
				ДанныеФото.Записать(ИмяФайла);
				ОписаниеТовара.Вставить("Фото", СтрШаблон(ШаблонИмениКартинки, "", ПолучитьРазделительПути(), Выборка.Идентификатор));	
			КонецЕсли;
			
			ОписаниеТоваров.Добавить(ОписаниеТовара);
			
		КонецЦикла;
		
		ШаблонИмениФайлаВыгрузки = "%1%2export.json";
		ИмяФайлаВыгрузки = СтрШаблон(ШаблонИмениФайлаВыгрузки, ИмяКаталога, ПолучитьРазделительПути());
		
		Запись = Новый ЗаписьJSON;
		Запись.ОткрытьФайл(ИмяФайлаВыгрузки);
		ЗаписатьJSON(Запись, ОписаниеТоваров);
		Запись.Закрыть();
		
		Архиватор = Новый ЗаписьZipФайла;
		Архиватор.Добавить(ИмяКаталога + ПолучитьРазделительПути() + "*.*",, 
		РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
		ДанныеАрхива = Архиватор.ПолучитьДвоичныеДанные();
		
		УдалитьФайлы(ИмяКаталога);
		
		Возврат ПоместитьВоВременноеХранилище(ДанныеАрхива);
		
	КонецФункции

#КонецОбласти