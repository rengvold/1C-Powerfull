&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Дата = ТекущаяДата();
	
	СоздатьИзмеренияПланировщика();
	
	ОбновитьПериодОтображенияПланировщика();
	
	ЗаполнитьЖурналЗаписей();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПериодОтображенияПланировщика()
	Начало = НачалоДня(Дата) + 9*3600;
	Конец = НачалоДня(Дата) + 19 *3600;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(Начало, Конец);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЖурналЗаписей() 
	
	Планировщик.Элементы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписьКлиента.Дата КАК Дата,
		|	ЗаписьКлиента.Клиент КАК Клиент,
		|	ЗаписьКлиента.Тренер КАК Тренер,
		|	ЗаписьКлиента.СуммаДокумента КАК СуммаДокумента,
		|	ЗаписьКлиента.Комментарий КАК Комментарий,
		|	ЗаписьКлиента.ДатаОкончания КАК ДатаОкончания,
		|	ЗаписьКлиента.Длительность КАК Длительность,
		|	ЗаписьКлиента.Ссылка КАК Ссылка,
		|	ЗаписьКлиента.Клиент.Телефон КАК Телефон,
		|	ЗаписьКлиента.ОказываемыеУслуги.(
		|		ПРЕДСТАВЛЕНИЕ(ЗаписьКлиента.ОказываемыеУслуги.Услуга) КАК Услуга
		|	) КАК ОказываемыеУслуги,
		|	ЗаписьКлиента.Цвет КАК Цвет
		|ИЗ
		|	Документ.ЗаписьКлиента КАК ЗаписьКлиента
		|ГДЕ
		|	ЗаписьКлиента.Проведен
		|	И ЗаписьКлиента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";  
	
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания",КонецДня(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗначенияИзмерений = Новый Соответствие;
		ЗначенияИзмерений.Вставить("Тренер", Выборка.Тренер);
		
		МассивСтрок = Новый Массив;
		
		ЖирныйШрифт =  Новый Шрифт(,,Истина);
		
		ПредставлениеКлиента = Строка(Выборка.Клиент) + ", " + Выборка.Телефон;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ПредставлениеКлиента,ЖирныйШрифт));
		МассивСтрок.Добавить(Символы.ПС);
		Если ЗначениеЗаполнено(Выборка.Комментарий) Тогда
			МассивСтрок.Добавить("-- " + Выборка.Комментарий);
		КонецЕсли;

		ЭлементПланировщика = Планировщик.Элементы.Добавить(Выборка.Дата, Выборка.ДатаОкончания);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений);
		ЭлементПланировщика.Значение = Выборка.Ссылка;
		ЭлементПланировщика.Текст = Новый ФорматированнаяСтрока(МассивСтрок);
		Цвет = Выборка.Цвет.Получить();
		СтандартныйЦвет = Новый Цвет(0, 0, 0);
		Если Цвет = СтандартныйЦвет Тогда
			Цвет = WebЦвета.НебесноГолубой
		КонецЕсли;
			
		ЭлементПланировщика.ЦветФона = Цвет;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьИзмеренияПланировщика()
	Измерение = Планировщик.Измерения.Добавить("Тренер");
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Наименование КАК Наименование,
		|	Сотрудники.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Тренер = ИСТИНА
		|	И Сотрудники.ПометкаУдаления = ЛОЖЬ"; 
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
				
		ЗначениеИзмерения = Измерение.Элементы.Добавить(Выборка.Ссылка);
		ЗначениеИзмерения.Текст = Выборка.Наименование;
		ЗначениеИзмерения.ЦветРамки = WebЦвета.Синий;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ОбновитьПериодОтображенияПланировщика();
	ЗаполнитьЖурналЗаписей();
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Дата", Начало);
	ЗначенияЗаполнения.Вставить("ДатаОкончания", Конец);
	ЗначенияЗаполнения.Вставить("Тренер", ЗначенияИзмерений["Тренер"]);
	
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ЗаписьКлиента.ФормаОбъекта", СтруктураПараметров)
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Изменение_Запись" Тогда
		ЗаполнитьЖурналЗаписей();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	
	СтруктураПараметров = Новый Структура("Ключ", ЭлементПланировщика.Значение);
	ОткрытьФорму("Документ.ЗаписьКлиента.ФормаОбъекта", СтруктураПараметров)
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	
	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить("Дата", ЭлементПланировщика.Начало); 
	ЗначенияРеквизитов.Вставить("ДатаОкончания", ЭлементПланировщика.Конец);
	ЗначенияРеквизитов.Вставить("Тренер", ЭлементПланировщика.ЗначенияИзмерений["Тренер"]);
	
	ОбновитьДанныеЗаписи(ЭлементПланировщика.Значение, ЗначенияРеквизитов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьДанныеЗаписи(Запись, ЗначенияРеквизитов)
	
	ОбъектЗаписи = Запись.ПолучитьОбъект();
	
	ЕстьИзменения = Ложь;
	Для каждого Реквизит Из ЗначенияРеквизитов Цикл
		Если ОбъектЗаписи[Реквизит.Ключ] <> Реквизит.Значение Тогда
			ЕстьИзменения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		ЗаполнитьЗначенияСвойств(ОбъектЗаписи, ЗначенияРеквизитов);
		ОбъектЗаписи.Длительность = (ОбъектЗаписи.ДатаОкончания - ОбъектЗаписи.Дата) / 60;
		ОбъектЗаписи.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры