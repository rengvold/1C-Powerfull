#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура Прочитать(Команда)
	
	ПараметрыПомещения = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыПомещения.Фильтр = "Эксель (*.xlsx)|*.xlsx";
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыПомещения);
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьФайлНаСервере(Результат.Адрес)

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьФайлНаСервере(Адрес)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	
	ИмяФайла = ПолучитьИмяВременногоФайла("xlsx");
	
	ДвоичныеДанные.Записать(ИмяФайла);
	
	ТабличныйДокумент.Прочитать(ИмяФайла);
	
	Попытка
		УдалитьФайлы(ИмяФайла);
	Исключение
		// Файл уже удален:
	КонецПопытки;
	
	РезультатПоиска = ТабличныйДокумент.Области.Найти("Цены");
	
	Если РезультатПоиска = Неопределено Тогда
		Сообщить("Лист с именем Цены не найден, проверьте исходный файл");
		Возврат;
	КонецЕсли;
	
	ОбластьЦены = ТабличныйДокумент.ПолучитьОбласть("Цены");

	Для НомерСтроки = 2 По ОбластьЦены.ВысотаТаблицы Цикл
		НоваяСтрока = Цены.Добавить();
		НоваяСтрока.Товар = ОбластьЦены.Область(НомерСтроки, 1).Текст;
		НоваяСтрока.Количество = ОбластьЦены.Область(НомерСтроки, 2).Текст;
		НоваяСтрока.Цена = ОбластьЦены.Область(НомерСтроки, 3).Текст;
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти