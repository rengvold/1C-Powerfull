
Функция ПроверитьДоступность(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("Ok");
	Возврат Ответ;
КонецФункции

Функция exchangeВыполнитьОбмен(Запрос) 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		
		ПроверитьЭтотУзел();
		
		ИдентификаторУстройства = Запрос.Заголовки.Получить("X-Mobile-ID");
		
		ВходящееСообщение = Запрос.ПолучитьТелоКакСтроку();
		
		Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
			ВызватьИсключение "Не указан идентификатор устройства";
		КонецЕсли;
		
		ТелоОтвета = ОбменСМобильным.ПодготовитьОтвет(ВходящееСообщение, ИдентификаторУстройства);
		
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
		Возврат Ответ;
	Исключение
		ЗаписьЖурналаРегистрации("Мобильный.ОшибкаОбмена", УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

Процедура ПроверитьЭтотУзел()
	
	ЭтотУзел = ПланыОбмена.Мобильный.ЭтотУзел();
	
	ЭтотУзелОбъект = ЭтотУзел.ПолучитьОбъект();
	
	Если ЗначениеЗаполнено(ЭтотУзелОбъект.Код) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотУзелОбъект.Код = "ЦентральнаяБаза";
	ЭтотУзелОбъект.Наименование = "Центральная база";
	
	ЭтотУзелОбъект.Записать();
	
КонецПроцедуры
