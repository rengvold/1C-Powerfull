Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ОстаткиТоваров Расход
	Движения.ОстаткиТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаПроданныеТовары Из ПроданныеТовары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = ТекСтрокаПроданныеТовары.Товар;
		Движение.Склад = Склад;
		Движение.Количество = ТекСтрокаПроданныеТовары.Количество;
	КонецЦикла;

	// регистр ПродажаТоваров 
	Движения.ПродажаТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаПроданныеТовары Из ПроданныеТовары Цикл
		Движение = Движения.ПродажаТоваров.Добавить();
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Товар = ТекСтрокаПроданныеТовары.Товар;
		Движение.Количество = ТекСтрокаПроданныеТовары.Количество;
		Движение.Цена = ТекСтрокаПроданныеТовары.Цена;
		Движение.Сумма = ТекСтрокаПроданныеТовары.Сумма;
	КонецЦикла;

	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиТоваровОстатки.Товар КАК Товар,
	               |	-ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваров.Остатки(
	               |			&Период,
	               |			Товар В
	               |					(ВЫБРАТЬ
	               |						РеализацияТоваровПроданныеТовары.Товар КАК Товар
	               |					ИЗ
	               |						Документ.РеализацияТоваров.ПроданныеТовары КАК РеализацияТоваровПроданныеТовары
	               |					ГДЕ
	               |						РеализацияТоваровПроданныеТовары.Ссылка = &Ссылка)
	               |				И Склад = &Склад) КАК ОстаткиТоваровОстатки
	               |ГДЕ
	               |	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
	
	 Запрос.УстановитьПараметр("Ссылка", Ссылка);
	 Запрос.УстановитьПараметр("Склад", Склад);
	 Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	 
	 
	 Выборка = Запрос.Выполнить().Выбрать();
	 
	 Пока Выборка.Следующий() Цикл
		 
		  Отказ = Истина;
		  ШаблонСообщения = "Недостаточно товара %1, не хватает %2";
		  
		  ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.Товар, Выборка.КоличествоОстаток);
		  
		  Сообщение = Новый СообщениеПользователю;
		  Сообщение.Текст = ТекстСообщения;
		  Сообщение.Сообщить();
		  
	  КонецЦикла;
	  
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПараметрыСеанса.ТекущийСотрудник;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПараметрыСеанса.ТекущийСотрудник;
КонецПроцедуры




