&НаКлиенте
Процедура ПроданныеТоварыКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ПроданныеТовары.ТекущиеДанные;
	РаботаСДокументами.РассчитатьСумму(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроданныеТоварыЦенаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ПроданныеТовары.ТекущиеДанные;
	РаботаСДокументами.РассчитатьСумму(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроданныеТоварыТоварПриИзменении(Элемент) 
	
	// Получить текущую строку табличной части.
	СтрокаТабличнойЧасти = Элементы.ПроданныеТовары.ТекущиеДанные;
	
	// Установить цену.
	СтрокаТабличнойЧасти.Цена = РаботаСРегистрами.РозничнаяЦенаПродажиТовара(Объект.Дата,СтрокаТабличнойЧасти.Товар);       
	
	// Пересчитать сумму строки
	РаботаСДокументами.РассчитатьСумму(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроданныеТоварыПриИзменении(Элемент)
	
	Объект.СуммаДокумента = Объект.ПроданныеТовары.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКОплате(Команда)
	ДокументЗаписан = Ложь;
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		ДокументЗаписан = Записать(ПараметрыЗаписи);
	Иначе
		ДокументЗаписан = Истина;
		
	КонецЕсли;
	
	Если ДокументЗаписан Тогда
		СтруктураПараметров = Новый Структура("Основание", Объект.Ссылка);
		ОткрытьФорму("Документ.ОплатаТоваров.ФормаОбъекта", СтруктураПараметров,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
  	ПараметрыФормы = Новый структура;
 	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь); 
 	ОткрытьФорму("Справочник.Товары.ФормаВыбора", ПараметрыФормы, Элементы.ПроданныеТовары);
КонецПроцедуры

&НаКлиенте
Процедура ПроданныеТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
  	СтандартнаяОбработка = Ложь;
  	Фильтр = Новый Структура;
 	Фильтр.Вставить("Товар", ВыбранноеЗначение); 
 	НайденныеСтроки = Объект.ПроданныеТовары.НайтиСтроки(Фильтр); 
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда  
		Возврат;
 	КонецЕсли; 
 	НоваяСтрока = Объект.ПроданныеТовары.Добавить(); 
	НоваяСтрока.Товар = ВыбранноеЗначение; 
 	НоваяСтрока.Количество = 1; 
	НоваяСтрока.Цена = РаботаСРегистрами.РозничнаяЦенаПродажиТовара(Объект.Дата, ВыбранноеЗначение);
 	НоваяСтрока.Сумма = НоваяСтрока.Цена;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПересчитатьВсеЦены(Команда)
	Если Не  ЗначениеЗаполнено(Объект.ПроданныеТовары) Тогда
  		Возврат; 
	КонецЕсли;
  	ТекстВопроса = "Цены во всех строках таблицы будут заменены на установленные по умолчанию. Продолжить?";
  	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
  	Если Ответ <> КодВозвратаДиалога.Да Тогда
  		Возврат 
	КонецЕсли;
  	СписокТоваров = Новый Массив;
  	Для Каждого Строка Из Объект.ПроданныеТовары Цикл
  		СписокТоваров.Добавить(Строка.Товар); 
	КонецЦикла;
  	ЦеныТоваров = РаботаСРегистрами.ЦеныПродажиТовараНаДату(Объект.Дата, СписокТоваров);
  	Для Каждого Строка из Объект.ПроданныеТовары Цикл
  		Цена = ЦеныТоваров.Получить(Строка.Товар);
		Если ЗначениеЗаполнено(Цена) Тогда
   			Строка.Цена = Цена;  
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПересчитатьВыбранныеЦены(Команда)
  	ВыделенныеСтроки = Элементы.ПроданныеТовары.ВыделенныеСтроки;
  	Если Не  ЗначениеЗаполнено(ВыделенныеСтроки) Тогда
  		Возврат; 
	КонецЕсли;
  	ТекстВопроса = "Цены в выбранных строках таблицы будут заменены на установленные по умолчанию. Продолжить?";
  	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
  	Если Ответ <> КодВозвратаДиалога.Да Тогда
  		Возврат 
	КонецЕсли;
  	СписокТоваров = Новый Массив;
  	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
  		ДанныеСтроки = Объект.ПроданныеТовары.НайтиПоИдентификатору(ИдентификаторСтроки);  
		СписокТоваров.Добавить(ДанныеСтроки.Товар);
 	КонецЦикла; 
 	ЦеныТоваров = РаботаСРегистрами.ЦеныПродажиТовараНаДату(Объект.Дата, СписокТоваров); 
	Для Каждого ИдентификаторСтроки из ВыделенныеСтроки Цикл  
		ДанныеСтроки = Объект.ПроданныеТовары.НайтиПоИдентификатору(ИдентификаторСтроки);
  		Цена = ЦеныТоваров.Получить(ДанныеСтроки.Товар);   
		Если ЗначениеЗаполнено(Цена) Тогда
   			ДанныеСтроки.Цена = Цена;  
		КонецЕсли;
 	КонецЦикла;  
КонецПроцедуры




