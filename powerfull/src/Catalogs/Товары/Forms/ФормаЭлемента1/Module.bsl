
&НаКлиенте
Процедура АдресФотоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	#Если МобильныйКлиент Тогда
		
		Если СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
			ДанныеФото = СредстваМультимедиа.СделатьФотоснимок();
			Если ДанныеФото = Неопределено Тогда
				Возврат;
			КонецЕсли;
			ДвоичныеДанные = ДанныеФото.ПолучитьДвоичныеДанные();
			АдресФото = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		КонецЕсли; 
		
	#Иначе
		
		ВыбратьФотоИзФайла();
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьФотоИзФайла()
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = "Изображение(*.png,*.jpg)|*.png;*.jpg";
	
	РезультатПомещения = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога,УникальныйИдентификатор);
	
	Если РезультатПомещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФото = РезультатПомещения.Адрес 
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)  
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ТекущийОбъект.Фото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФото));
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресФото = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "Фото");
	
КонецПроцедуры

